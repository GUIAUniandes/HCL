P -> program name begin BODY end
BODY -> SCOPE | EPSILON
SCOPE -> INSTRUCTION NEXTINSTR
NEXTINSTR -> INSTRUCTION NEXTINSTR | EPSILON
INSTRUCTION -> DO | IF | ASSIGNMENT | DEFINITION | FUNC | READ | print EXPR | skip | abort
FUNC -> func ARGS )
READ -> read( name )
DO -> do GUARDS od
IF -> if GUARDS fi
GUARDS -> GUARD NEXTGUARD
NEXTGUARD -> □ GUARD NEXTGUARD | EPSILON
GUARD -> EXPR → SCOPE
ASSIGNMENT -> VARLIST ← EXPRLIST
ARGS -> EXPRLIST | EPSILON
EXPRLIST -> EXPR NEXTEXPR
NEXTEXPR -> , EXPR NEXTEXPR | EPSILON
VARLIST -> name INDICES NEXTVAR
NEXTVAR -> , name INDICES NEXTVAR | EPSILON
DEFINITION -> var VARLIST : DIMENSIONS
DIMENSIONS -> TYPE | array DIMS of TYPE
TYPE -> boolean | int | char
DIMS -> [ INTERVALS ]
INTERVALS -> INTERVAL NEXTINTERVAL
INTERVAL -> number .. number
NEXTINTERVAL -> , INTERVAL NEXTINTERVAL | EPSILON
EXPR -> Q N
N -> ∧ Q N | EPSILON
Q -> Λ K
K -> ∨ Λ K | EPSILON
Λ -> ¬ Λ | Σ
Σ -> E Y
Y -> ≥ E Y | ≤ E Y | > E Y | < E Y | ≠ E Y | = E Y | EPSILON
E -> T A
A -> + T A | - T A | EPSILON
T -> F B
B -> * F B | / F B | ^ F B | % F B | EPSILON
F -> - F | ( EXPR ) | V | FUNC
V -> number | name INDICES | ∞
INDICES -> [ INDEXLIST ] | EPSILON
INDEXLIST -> E NEXTIDX
NEXTIDX -> : E NEXTIDX | , E NEXTIDX | EPSILON
